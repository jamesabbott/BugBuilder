#!/usr/bin/env perl

######################################################################
#
#
# Wrapper for SIS to permit use via BugBuilder scaffolding stages
#
# This file is part of BugBuilder (https://github.com/jamesabbott/BugBuilder)
# and is distributed under the Artistic License 2.0 - see accompanying LICENSE
# file for details
#
######################################################################

=pod

=head1 NAME

run_sis

=head1 SYNOPSIS

run_sis --tmpdir BugBuilder_working_directory --reference reference_genome.fasta [--help]

=head1 DESCRIPTION

Wrapper for SIS scaffolder to permit use withing BugBuilder's scaffolding configuration.
SIS makes use of MUMmer to carry out alignments, and post-processes the show_coords output. 
These MUMmer stages need running prior to executing SIS itself. Following SIS execution the
generated scaffolds (which consist of ordered contigs, with one scaffold per fasta file) 
are reprocessed into a multifasta file of 'N' gapped scaffold sequences.

=head1 REQUIRED ARGUMEMNTS

=over 4

=item B<tmpdir>: BugBuilder working directory

=item B<scaff_dir>: Directory within tmpdir for scaffolding these sequences...

=item B<reference>: Fasta formatted reference genome for aligning contigs against

=item B<contigs>: Fasta formatted file of contig sequences to scaffold

=back

=head1 OPTIONAL ARGUMENTS

=over 4

=item B<help>: display short help text

=item B<man>: display full documentation

=back

=head1 REPORTING BUGS

Please report any bugs/issues via github:
https://github.com/jamesabbott/BugBuilder/issues/new

=head1 AUTHOR - James Abbott

Email j.abbott@dundee.ac.uk

=cut

use warnings;
use strict;

use FindBin;
use YAML::XS qw(LoadFile);
use Getopt::Long;
use Pod::Usage;
use Carp qw(croak cluck);
use Bio::SeqIO;
use File::Basename;

{

    my ( $help, $man, $reference, $contigs, $tmpdir, $scaff_dir );

    my $result = GetOptions(
                             'reference:s' => \$reference,
                             'contigs:s'   => \$contigs,
                             'tmpdir=s'    => \$tmpdir,
                             'scaff_dir=s' => \$scaff_dir,
                             'help'        => \$help,
                             'man'         => \$man,
                           );

    croak "\nUnknown argument: @ARGV" if "@ARGV";
    pod2usage( verbose => 2 ) if ($man);
    pod2usage( verbose => 1 )
      if ( $help || !$tmpdir || !$scaff_dir || !$reference || !$contigs );

    chdir "$scaff_dir" or croak "Could not chdir to $scaff_dir:$!";

    croak "\n$reference does not exist" unless ( -e $reference );
    croak "\n$contigs does not exist"   unless ( -e $contigs );

    my $cmd = "nucmer $reference $contigs -p $scaff_dir/sis 2>&1 > $scaff_dir/nucmer.log";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd =  "delta-filter -1 $scaff_dir/sis.delta 2>$scaff_dir/delta-filter.log > $scaff_dir/sis.filter";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd =  "show-coords $scaff_dir/sis.filter 2>$scaff_dir/show-coords.log > $scaff_dir/sis.coords";
    system($cmd) == 0 or croak "Error executing $cmd: $!";

    $cmd = "sis.py $scaff_dir/sis.coords > $scaff_dir/sis.sis";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd = "multifasta.py $scaff_dir/sis.sis $contigs";
    system($cmd) == 0 or croak "Error executing $cmd: $!";

    my $ref_id = basename($scaff_dir);

    #$ref_id=~s/SIS_//;

    # Build a multifasta file of N-gapped scaffolds
    opendir SIS, "$scaff_dir" or die "Error opening $scaff_dir: $!";
    my @scaffolds = grep /.fna$/, readdir SIS;
    close SIS;

    my ( @multi, @singletons );

    foreach my $scaffold (@scaffolds) {

        my $contig_count = `grep -c '>' $scaff_dir/$scaffold`;
        chomp $contig_count;

        if ( $contig_count > 1 ) {
            push @multi, $scaffold;
        }
        else {
            push @singletons, $scaffold;
        }
    }

    my $multiFastaIO = Bio::SeqIO->new( -format => 'fasta',
                                        -file   => ">$scaff_dir/scaffolds.fasta" );

    my $i = 0;
    foreach my $scaffold (@multi) {

        my $io = Bio::SeqIO->new( -format => 'fasta',
                                  -file   => "$scaff_dir/$scaffold" );
        my $scaffold = Bio::Seq->new( -display_id => "scaffold_${ref_id}" . ++$i );
        while ( my $seq = $io->next_seq ) {
            if ( $scaffold->length() > 0 ) {
                $scaffold->seq( $scaffold->seq() . 'N' x 100 . $seq->seq() );
            }
            else {
                $scaffold->seq( $seq->seq() );
            }
        }
        $multiFastaIO->write_seq($scaffold);
    }

    #  append singleton scaffolds to scaffolds.fasta
    foreach my $scaffold (@singletons) {
        my $io = Bio::SeqIO->new( -format => 'fasta',
                                  -file   => "$scaff_dir/$scaffold" );
        my $seq = $io->next_seq();
        $seq->display_id( "scaffold_${ref_id}" . ++$i );
        $multiFastaIO->write_seq($seq);

    }

    open SIS_OUTPUT, "$scaff_dir/sis.sis"         or croak "Error opening $scaff_dir/sis.sis: $!";
    open CONTIG_IDS, ">$scaff_dir/sis.contig_ids" or croak "Error opening $scaff_dir/sis.contig_ids: $!";
    while ( my $line = <SIS_OUTPUT> ) {
        next if ( $line =~ /^>|^$/ );
        my $contig = ( split( / /, $line ) )[0];
        print CONTIG_IDS $contig, "\n";
    }
    close SIS_OUTPUT;
    close CONTIG_IDS;

}
