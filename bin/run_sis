#!/bin/env perl

######################################################################
#
# $HeadURL: https://bss-srv4.bioinformatics.ic.ac.uk/svn/BugBuilder/trunk/bin/run_sis $
# $Author: jamesa $
# $Revision: 128 $
# $Date: 2015-11-08 20:34:21 +0000 (Sun, 08 Nov 2015) $
#
# Wrapper for SIS to permit use via BugBuilder scaffolding stages
#
######################################################################

=pod

=head1 NAME

run_sis

=head1 SYNOPSIS

run_sis --tmpdir BugBuilder_working_directory --reference reference_genome.fasta [--help]

=head1 DESCRIPTION

Wrapper for SIS scaffolder to permit use withing BugBuilder's scaffolding configuration.
SIS makes use of MUMmer to carry out alignments, and post-processes the show_coords output. 
These MUMmer stages need running prior to executing SIS itself. Following SIS execution the
generated scaffolds (which consist of ordered contigs, with one scaffold per fasta file) 
are reprocessed into a multifasta file of 'N' gapped scaffold sequences.

=head1 REQUIRED ARGUMEMNTS

=over 4

=item B<tmpdir>: BugBuilder working directory

=item B<scaff_dir>: Directory within tmpdir for scaffolding these sequences...

=item B<reference>: Fasta formatted reference genome for aligning contigs against

=item B<contigs>: Fasta formatted file of contig sequences to scaffold

=back

=head1 OPTIONAL ARGUMENTS

=over 4

=item B<help>: display short help text

=item B<man>: display full documentation

=back

=head1 REPORTING BUGS

Please report any bugs/issues via github:
https://github.com/jamesabbott/BugBuilder/issues/new

=head1 AUTHOR - James Abbott

Email j.abbott@imperial.ac.uk

=cut

use warnings;
use strict;

use FindBin;
use YAML::XS qw(LoadFile);
use Getopt::Long;
use Pod::Usage;
use Carp qw(croak cluck);
use Bio::SeqIO;
use File::Basename;

{

    my $config = LoadFile("$FindBin::Bin/../etc/BugBuilder.yaml");

    my ( $help, $man, $reference, $contigs, $tmpdir, $scaff_dir );

    my $result = GetOptions(
                             'reference:s' => \$reference,
                             'contigs:s'   => \$contigs,
                             'tmpdir=s'    => \$tmpdir,
                             'scaff_dir=s' => \$scaff_dir,
                             'help'        => \$help,
                             'man'         => \$man,
                           );

    croak "\nUnknown argument: @ARGV" if "@ARGV";
    pod2usage( verbose => 2 ) if ($man);
    pod2usage( verbose => 1 )
      if ( $help || !$tmpdir || !$scaff_dir || !$reference || !$contigs );

    chdir "$scaff_dir" or croak "Could not chdir to $scaff_dir:$!";

    croak "\n$reference does not exist" unless ( -e $reference );
    croak "\n$contigs does not exist"   unless ( -e $contigs );

    my $cmd = $config->{'mummer_dir'}
      . "/nucmer $reference $contigs -p $scaff_dir/sis 2>&1 > $scaff_dir/nucmer.log";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd = $config->{'mummer_dir'}
      . "/delta-filter -1 $scaff_dir/sis.delta 2>$scaff_dir/delta-filter.log > $scaff_dir/sis.filter";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd = $config->{'mummer_dir'}
      . "/show-coords $scaff_dir/sis.filter 2>$scaff_dir/show-coords.log > $scaff_dir/sis.coords";
    system($cmd) == 0 or croak "Error executing $cmd: $!";

    $cmd = $config->{'sis_dir'} . "sis.py $scaff_dir/sis.coords > $scaff_dir/sis.sis";
    system($cmd) == 0 or croak "Error executing $cmd: $!";
    $cmd = $config->{'sis_dir'} . "multifasta.py $scaff_dir/sis.sis $contigs";
    system($cmd) == 0 or croak "Error executing $cmd: $!";

	my $ref_id = basename($scaff_dir);
	#$ref_id=~s/SIS_//;

    # Build a multifasta file of N-gapped scaffolds
    opendir SIS, "$scaff_dir" or die "Error opening $scaff_dir: $!";
    my @scaffolds = grep /.fna$/, readdir SIS;
    close SIS;

    my ( @multi, @singletons );

    foreach my $scaffold (@scaffolds) {

        my $contig_count = `grep -c '>' $scaff_dir/$scaffold`;
        chomp $contig_count;

        if ( $contig_count > 1 ) {
            push @multi, $scaffold;
        }
        else {
            push @singletons, $scaffold;
        }
    }

    my $multiFastaIO = Bio::SeqIO->new( -format => 'fasta',
                                        -file   => ">$scaff_dir/scaffolds.fasta" );

    my $i = 0;
    foreach my $scaffold (@multi) {

        my $io = Bio::SeqIO->new( -format => 'fasta',
                                  -file   => "$scaff_dir/$scaffold" );
        my $scaffold = Bio::Seq->new( -display_id => "scaffold_${ref_id}" . ++$i );
        while ( my $seq = $io->next_seq ) {
            if ( $scaffold->length() > 0 ) {
                $scaffold->seq( $scaffold->seq() . 'N' x 100 . $seq->seq() );
            }
            else {
                $scaffold->seq( $seq->seq() );
            }
        }
        $multiFastaIO->write_seq($scaffold);
    }

    #  append singleton scaffolds to scaffolds.fasta
    foreach my $scaffold (@singletons) {
        my $io = Bio::SeqIO->new( -format => 'fasta',
                                  -file   => "$scaff_dir/$scaffold" );
        my $seq = $io->next_seq();
        $seq->display_id( "scaffold_${ref_id}" . ++$i );
        $multiFastaIO->write_seq($seq);

    }
	
    open SIS, "$scaff_dir/sis.sis" or croak "Error opening $scaff_dir/sis.sis: $!";
    open CONTIG_IDS, ">$scaff_dir/SIS.contig_ids" or croak "Error opening $scaff_dir/sis.contig_ids: $!";
    while ( my $line = <SIS> ) {
        next if ( $line =~ /^>|^$/ );
        my $contig = ( split( / /, $line ) )[0];
	print CONTIG_IDS $contig, "\n";
    }
    close SIS;
	close CONTIG_IDS;


}
